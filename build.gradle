plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group 'io.rqndomhax'
version '1.0-SNAPSHOT'
description 'RqndomUHC API by RqndomHax'

compileJava.getOptions().setEncoding("UTF-8")

repositories {
    mavenCentral()
    maven { url 'https://repo.dmulloy2.net/repository/public/' }
    mavenLocal()
}

dependencies {
    compileOnly 'org.spigotmc:spigot:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.7.0'
    implementation 'xyz.xenondevs:particle:1.5.1'
    implementation 'fr.mrmicky:fastboard:1.2.0'
}

java {
    withJavadocJar()
    withSourcesJar()
}

shadowJar {
    relocate 'fr.mrmicky.fastboard', 'io.rqndomhax.fastboard'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'rqndomuhc-api'
            artifact shadowJar
            pom {
                name = 'RqndomUHC'
                description = 'An API for Minecraft UHC Plugins'
                url = 'https://github.com/RqndomHax/RqndomUHC-API'
                developers {
                    developer {
                        id = 'rqndomhax'
                        name = 'RqndomHax'
                        email = 'paul.codepro@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/RqndomHax/RqndomUHC-API.git'
                    developerConnection = 'scm:git:git@github.com:RqndomHax/RqndomUHC-API.git'
                    url = 'https://github.com/RqndomHax/RqndomUHC-API'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/releases/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            allowInsecureProtocol = false
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
